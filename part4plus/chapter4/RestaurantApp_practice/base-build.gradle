
/**  아래 처럼 바꿔야함
 plugins {
 id 'com.android.application'
 id 'org.jetbrains.kotlin.android'
 id 'androidx.navigation.safeargs.kotlin'
 id 'dagger.hilt.android.plugin'
 id 'kotlin-android'
 id 'kotlin-kapt'
 }
 */

apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    // 강의에서는 namespace 없어도 된다고했지만 AGP(Android Gradle Plugin) 7.0 이상 부터는 있으면 좋음
    // 원래는 모듈마다 있으면 좋음
    compileSdk 33

    defaultConfig {
        minSdk 28
        targetSdk 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        debug {
            isDefault = true
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }

    lint {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        jniLibs {
            excludes += ['META-INF/*', '**/kotlin/**']
        }
        resources {
            excludes += ['META-INF/*', '**/kotlin/**', '**/*.txt']
        }
    }

    buildFeatures {
        viewBinding true
        compose true
        buildConfig = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.1'
    }
}



dependencies {
    // Hilt
    api "com.google.dagger:hilt-android:$hilt_version"
    api "androidx.hilt:hilt-navigation-fragment:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Coil - 이번 강의에서 Coil을 공통으로 사용하는 곳이 많아서 base로 뺌
    implementation "io.coil-kt:coil-compose:$coil_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Kotlin
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version"))
    api "androidx.core:core-ktx:$kotlin_version"

    // Compose
    api "androidx.compose.ui:ui:$compose_ui_version"
    api "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    api "androidx.compose.material:material:$compose_material_version"
    api "androidx.activity:activity-compose:$activity_compose_version"

    // LifecCycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}
